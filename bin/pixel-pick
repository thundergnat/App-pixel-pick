#!/usr/bin/env perl6

my $version = v0.3;

# Control-C exit
signal(SIGINT).tap: { sleep .3; cleanup(); print "\n" xx 50, "\e[H\e[J"; exit(0) }

# Control-Z exit
signal(SIGTSTP).tap: { sleep .3; print "\r  \n"; cleanup(); exit(0) }

my ($x, $y);
my ($lx, $ly) = 0, 0;
my $indent = '    ';
my ($red, $green, $blue);

multi MAIN () {
    use X11::libxdo;
    my $xdo = Xdo.new;
    loop {
        sleep .1;
        ($x, $y, $) = $xdo.get-mouse-location;
        next if $lx == $x and $ly == $y;
        ($lx, $ly) = $x, $y;
        get-pixel();
        display();
    }
    $xdo = ();
}
$green //= $red;
my %*SUB-MAIN-OPTS = :named-anywhere;

multi MAIN (
    Int $X,    #= Integer x coodinte to pick
    Int $Y,    #= Integer y coordinate to pick
    $q = False #= Boolean "quiet" mode, set to 'h' for hex values
  ) {
    ($x, $y) = +$X, +$Y;
    get-pixel();

    if $q {
        if $q.lc eq 'h' {
             printf "%02X:%02X:%02X\n", $red,$green,$blue;
         } else {
             printf "%03d:%03d:%03d\n", $red,$green,$blue;
         }
    } else {
        display();
        cleanup();
    }
    exit(0);
}

sub get-pixel {

    # import is installed as part of MagickWand. Most Linuxes
    # already has it. If not, need to install libmagickwand

    my $xcolor =
      qqx/import -window root -crop 1x1+{$x-1 max 0}+{$y-2 max 0} -depth 8 txt:-/
      .comb(/<?after '#'><xdigit> ** 6/);

    ($red, $green, $blue) = $xcolor.comb(2)».parse-base(16);
}

sub display {
    print "\e[?25l\e[48;2;0;0;0m\e[38;2;255;255;255m\e[H\e[J";
    printf "   v$version | x: %4d y: $y\n", $x;
    printf "{$indent}  RGB: %03d:%03d:%03d \n{$indent}  HEX: %02X:%02X:%02X\n",
            $red, $green, $blue, $red, $green, $blue;
    print "\e[38;2;{$red};{$green};{$blue}m{$indent}",
           ('█' x 18 xx 6).join: "\n{$indent}";
    print "\n\n";
}

sub cleanup { print "\e[0m\e[?25h" }
