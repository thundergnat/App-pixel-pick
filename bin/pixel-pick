#!/usr/bin/env perl6

my $version = v0.3;

# Control-C exit
signal(SIGINT).tap: { sleep .3; cleanup(); print "\n" xx 50, "\e[H\e[J"; exit(0) }

# Control-Z exit
signal(SIGTSTP).tap: { sleep .3; print "\r  \n"; cleanup(); exit(0) }

my ($x, $y);
my ($lx, $ly) = 0, 0;
my $indent = '    ';
my ($red, $green, $blue);

multi MAIN () {
    use X11::libxdo;
    my $xdo = Xdo.new;
    loop {
        sleep .1;
        ($x, $y, $) = $xdo.get-mouse-location;
        next if $lx == $x and $ly == $y;
        ($lx, $ly) = $x, $y;
        get-pixel();
        display();
    }
    $xdo = ();
}
$green //= $red;
my %*SUB-MAIN-OPTS = :named-anywhere;

multi MAIN (
    Int $X,    #= Integer x coodinte to pick
    Int $Y,    #= Integer y coordinate to pick
    $q = False #= Boolean "quiet" mode, set to 'h' for hex values
  ) {
    ($x, $y) = +$X, +$Y;
    get-pixel();

    if $q {
        if $q.lc eq 'h' {
             printf "%02X:%02X:%02X\n", $red,$green,$blue;
         } else {
             printf "%03d:%03d:%03d\n", $red,$green,$blue;
         }
    } else {
        display();
        cleanup();
    }
    exit(0);
}

sub get-pixel {

    my $fmt = '%[pixel:p{0,0}]'; #formatting string to retreive pixel info

    # import is installed as part of MagickWand. Most Linuxes
    # already has it. If not, need to install libmagickwand

    my $color = qqx/import -window root -crop 1x1+{$x-1 max 0}+{$y-2 max 0} -format '$fmt' info:-/;

    ($red, $green, $blue) = Any, Any, Any;
    ($red, $green, $blue) = $color.comb(/\d+/);
    $red   //= 0;
    $green //= $red;
    $blue  //= $red
}

sub display {
    print "\e[?25l\e[48;2;0;0;0m\e[38;2;255;255;255m\e[H\e[J";
    printf "   v$version | x: %4d y: $y\n", $x;
    printf "{$indent}  RGB: %03d:%03d:%03d \n{$indent}  HEX: %02X:%02X:%02X\n",
            $red, $green, $blue, $red, $green, $blue;
    print "\e[38;2;{$red};{$green};{$blue}m{$indent}",
           ('â–ˆ' x 18 xx 6).join: "\n{$indent}";
    print "\n\n";
}

sub cleanup { print "\e[0m\e[?25h" }
